# To use just, run the following command to install:
# curl --proto '=https' --tlsv1.2 -sSf https://just.systems/install.sh | bash -s -- --to /usr/local/bin/
# or
# brew install just

set windows-shell := ["pwsh.exe", "-c"]
tlaprefix := join(justfile_directory(), "bin")
tla2tools := join(tlaprefix, "tla2tools.jar")

default:
    just --list

prep-dev:
    @echo brew install java
    @echo install https://github.com/tlaplus/vscode-tlaplus
    @echo just prep-tla

trans SOURCE:
    java -cp {{tla2tools}} pcal.trans {{SOURCE}}

check SOURCE: (trans SOURCE)
    java -cp {{tla2tools}} tlc2.TLC {{SOURCE}}

doc SOURCE: (check SOURCE)
    java -cp {{tla2tools}} tla2tex.TLA -latexCommand 'tectonic --keep-logs' {{SOURCE}}

prep-tla:
    #!/usr/bin/env bash
    set -e
    mkdir -p {{tlaprefix}}
    cd {{tlaprefix}}
    if [ ! -f tla2tools.jar ]; then
        curl -L -o tla2tools.jar https://github.com/tlaplus/tlaplus/releases/download/v1.8.0/tla2tools.jar
    fi
    echo "âœ…TLA+ installed"
    # wget https://github.com/tlaplus/tlaplus/releases/download/v1.8.0/TLAToolbox-1.8.0-macosx.cocoa.x86_64.zip
    # unzip TLAToolbox-1.8.0-macosx.cocoa.x86_64.zip
    # mv TLAToolbox.app /Applications

prep-tectonic:
    # https://tectonic-typesetting.github.io/en-US/install.html
    curl --proto '=https' --tlsv1.2 -fsSL https://drop-sh.fullyjustified.net |sh